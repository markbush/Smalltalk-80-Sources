DisplayObject subclass: #InfiniteForm
  instanceVariableNames: 'patternForm'
  classVariableNames: ''
  poolDictionaries: ''
  category: 'Graphics-Display Objects'!
InfiniteForm class
  instanceVariableNames: ''!
"-----------------------------------"!
InfiniteForm comment:
'I represent a Form obtained by replicating a pattern form indefinitely in all directions.'!
"-----------------------------------"!
!InfiniteForm class methodsFor: 'instance creation'!
with: aForm
	^self new form: aForm!
"-----------------------------------"!
!InfiniteForm methodsFor: 'accessing'!
form: aForm
	patternForm _ aForm!
offset
	^0 @ 0!
"-----------------------------------"!
!InfiniteForm methodsFor: 'display box access'!
computeBoundingBox
	^0 @ 0 corner: SmallInteger maxVal @ SmallInteger maxVal!
"-----------------------------------"!
!InfiniteForm methodsFor: 'displaying'!
displayOn: aDisplayMedium at: aDisplayPoint clippingBox: clipRectangle rule: ruleInteger mask: aForm
	"This is the real display message, but it doesn't get used until the new display protocol is installed."
	| targetBox patternBox |
	(aForm == Form black and: [patternForm extent = (16 @ 16)])
		ifTrue:
			["Use patternForm as a mask for BitBlt"
			aDisplayMedium fill: clipRectangle rule: ruleInteger mask: patternForm]
		ifFalse:
			["Do it iteratively"
			targetBox _ aDisplayMedium boundingBox intersect: clipRectangle.
			patternBox _ patternForm boundingBox.
			(targetBox left truncateTo: patternBox width) to: targetBox right - 1 by: patternBox width do:
				[:x |
				(targetBox top truncateTo: patternBox height) to: targetBox bottom - 1 by: patternBox height do:
					[:y |
					patternForm displayOn: aDisplayMedium
						at: x @ y
						clippingBox: clipRectangle
						rule: ruleInteger
						mask: aForm]]]!
